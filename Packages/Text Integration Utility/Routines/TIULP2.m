TIULP2 ;SLC/JER - Functions determining privilege continued ;11/15/2006
 ;;1.0;TEXT INTEGRATION UTILITIES;**215**;Jun 20, 1997;Build 6
ISSURG(TIUDA) ;GET 8925.1 IEN
 NEW TIUY
 D ISSURG^TIUSROI(.TIUY,+$G(^TIU(8925,+TIUDA,0)))
 Q TIUY
SURGNOK(TIUDA) ;SEE IF IT IS AN NIR OR AR
 N TIUY,TIUS0,TIUSNAME
 S TIUY=0
 G SURGEX:'$L(TIUDA)
 G SURGEX:'$D(^TIU(8925,+TIUDA,0))
 S TIUS0=+$G(^TIU(8925,+TIUDA,0))
 G SURGEX:'$L(TIUS0)
 S TIUSNAME=$$PNAME^TIULC1(+TIUS0)
 G SURGEX:'$L(TIUSNAME)
 I TIUSNAME["NURSE INTRAOPERATIVE REPORT" S TIUY=1 G SURGEX
 I TIUSNAME["ANESTHESIA REPORT" S TIUY=1 G SURGEX
SURGEX Q TIUY
ACTION(TIUACTW) ;CHECK ACTION
 NEW TIUY S TIUY=0
 I (($G(TIUACTW)["DELETE RECORD")!($G(TIUACTW)["AMENDMENT")!($G(TIUACTW)["EDIT RECORD")!($G(TIUACTW)["ADDEND")) S TIUY=1 G ACTEX
ACTEX Q TIUY
SURMSG(TIUACTW) ;SET SURGERY ERROR MSG
 NEW TIUY,TIUDOC S TIUDOC=""
 I TIUACTW["DELETE" S TIUDOC="DELETE"
 I TIUACTW["EDIT" S TIUDOC="EDIT"
 I ((TIUACTW["ADDEND")!(TIUACTW["AMEND")) S TIUDOC="CREATE AN ADDENDUM FOR"
 S TIUY="You must use the Surgery Package to "_TIUDOC_" this Document"
 Q TIUY
IDMSG(TIUMSG) ;SET DELETE ID MSG
 S TIUMSG="You may NOT delete this parent ID note.  It has child ID notes attached. "
 S TIUMSG=TIUMSG_"If you need to delete this note you must first detach the Child from the Parent note."
 I $G(XQY0)["OR CPRS GUI CHART" D                 ;DBIA 3356
 . S TIUMSG=TIUMSG_" Select Action/Detach from Interdisciplinary note to accomplish this."
 E  S TIUMSG=TIUMSG_" Select the Child note from the LM screen, then select Interdiscipl'ry Note."
 Q
